buildscript
{
    repositories
    {
        jcenter()
        google()
    }

    dependencies
    {
        // 4.1.2 is the minimum version to support native debug symbols file
        // https://developer.android.com/studio/build/shrink-code#android_gradle_plugin_version_41_or_later
        classpath 'com.android.tools.build:gradle:4.1.2'
    }
}

allprojects
{
    repositories
    {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.application'

android
{
    compileSdkVersion 'android-Tiramisu'
    buildToolsVersion '30.0.2'
    ndkVersion '23.0.7599858'
    externalNativeBuild
    {
        ndkBuild
        {
            path 'Android.mk'
        }
    }

    defaultConfig
    {
        minSdkVersion 16
        targetSdkVersion 29
        externalNativeBuild
        {
            ndkBuild
            {
                arguments 'APP_PLATFORM=android-16', 'APP_STL=c++_static', '-j13'
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            }
        }
        def runTasks = gradle.startParameter.taskNames
        if ('bundleRelease' in runTasks)
        {
            // use SYMBOL_TABLE if too large later (max limit on google play is 300MB)
            ndk.debugSymbolLevel 'FULL'
        }
    }

    buildTypes
    {
        debug
        {
            debuggable true
            jniDebuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            externalNativeBuild
            {
                ndkBuild
                {
                    arguments 'NDK_DEBUG=1'
                    cFlags '-O0'
                    cppFlags '-O0'
                }
            }
        }
    }

    sourceSets
    {
        main
        {
            manifest.srcFile 'AndroidManifest.xml'
            jniLibs.srcDirs = ['libs']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

dependencies
{
    implementation 'org.minidns:minidns-hla:0.3.3'
}
